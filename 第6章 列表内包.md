# 列表内包

列表内包（list comprehension），具体指的是 Haskell 提供一种精简的语法来对列表中的元素进行处理，如匹配，过滤，应用函数或者组合，这并不是 Haskell 的新功能，而仅仅只是语法糖。

## 列表生成器

```haskell
{-
  x<-[0..100] 为生成器
  x<10则是对 x 的条件限制
  x^2则对符合条件的 x 进行转换，应用平方函数，从而得到了一个新的列表
-}
>>> [x^2|x<-[0..100], x<10]
[0,1,4,9,16,25,36,49,64,81]

--生成列表的时候可以使用多个生成器，但是生成的过程是有一定的顺序的，写在后边的生成器将会被先遍历。
>>> [(x,y)|x<-[0..5],y<-"abc"]
[(0,'a'),(0,'b'),(0,'c'),(1,'a'),(1,'b'),(1,'c'),(2,'a'),(2,'b'),(2,'c'),(3,'a'),(3,'b'),(3,'c'),(4,'a'),(4,'b'),(4,'c'),(5,'a'),(5,'b'),(5,'c')]

--用列表生成器来定义 map 函数
map' f xs = [f x | x <- xs]

>>> map' succ [0..10]
[1,2,3,4,5,6,7,8,9,10,11]

--用列表内包来定义 filter 函数
filter' f xs = [x | x <-xs, f x]

>>> filter' even [0..10]
[0,2,4,6,8,10]

--此外，限定的条件可能不止一个，可以再加入一些限定的条件，这些限定条件之前是且的关系。
[x | x<-[0..], even x, x > 10]

--使用列表内包时，如果用不到生成器中产生的值，可以使用通配符来匹配。
length' xs = sum [1 | _ <- xs]
```

## 并列的列表内包与一般的列表内包

并列的列表内包（parallel list comprehension）相当于列表内包的一个扩展。普通的列表内包如[(x,y) | x <- [1,2,3], y <- [4,5,6]]相当于对笛卡尔积，而如果我们项得到[(1,4),  (2,5), (3,6)]就需要zipWith函数了，更为复杂的还需要使用zipWith3、zipWith4等等，但是有了并列的列表内包我就可以很容易的表达它们了。  GHC 中需要使用 ParallelListComp 语言扩展来做到：

```haskell
:set -XParallelListComp

>>> [(x,y) | x <- [1,2,3] | y <- [4,5,6]]
[(1,4),(2,5),(3,6)]

>>> [(x,y,z)|x<-[1,2,3], y<-[4,5,6] | z<-[7,8,9]]
[(1,4,7),(1,5,8),(1,6,9)]

--并列的列表内包的本质是 zip 函数
--上面这段代码可以通过以下代码来展示
>>> [(x,y)|x<-[1,2,3], y<-[4,5,6]]
[(1,4),(1,5),(1,6),(2,4),(2,5),(2,6),(3,4),(3,5),(3,6)]

--这里 z 的个数不够，所以需要写成:
>>> [(x,y,z)|x<-[1,2,3], y<-[4,5,6] | z<-cycle [7,8,9]]
[(1,4,7),(1,5,8),(1,6,9),(2,4,7),(2,5,8),(2,6,9),(3,4,7),(3,5,8),(3,6,9)]
```

除了并列的列表内包，GHC还提供了一般化的列表内包（generalised list comprehension），它与数据库语言SQL十分相似，可以非常方便地操作列表中的元组。

```haskell
-- paraListComp.hs
{-# LANGUAGE ParallelListComp, TransformListComp #-}
import GHC.Exts

table = [("Hangzhou", "MP4", 243),
         ("Hangzhou", "CD", 925),
	     ("Beijing", "MP4", 157),
	     ("Beijing", "CD", 536),
         ("Shanghai", "MP4", 784),
         ("Shanghai", "CD", 766)]

-- 按产品统计成本之和，并按成本和排序
analysis = [(the product, sum cost)|
                 (city, product, cost) <- table,
		 then group by product using groupWith,
		 then sortWith by (sum cost)]
		 
>>> analysis
[("MP4",1184),("CD",2227)]
```

## 素数相关趣题

素数又称质数。对于一个整数，除以 1 或者本身时余数为0，那么这个数就是素数。

```haskell
-- 找出余数为 0 的数
factors :: Integral a => a -> [a]
factors n = [x | x <- [1..n], (mod n x) == 0]

-- 检验一个数是否为素数
isPrime :: Integral a => a -> Bool
isPrime n = factors n == [1, n]

-- 生成素数的列表
primes :: Integral a => a -> [a]
primes n = [x | x <- [1..n], isPrime x]

>>> primes 50
[2,3,5,7,11,13,17,19,23,29,31,37,41,43,47]

-- 上述生成素数的函数效率很低，因为每次求因数的时候，要从 1 ~ n 全部遍历一次。
{-
	优化版本
	1、除去 2 以外，所有的素数都必须时奇数
	2、素数必须是大于等于 2 的整数
	3、对于一个整数 N ，若它有一个因数为 p1，必须有另外一个因子 p2，是的 N = p1.p2，并且p1 与 p2一定分布在 √n （开方）的两端 或 p1=p2 =  √n （开方）；
-}

isPrime' :: Integral a => a -> Bool
isPrime' 1 = True
isPrime' 2 = True
isPrime' p = p > 1 && (all (\n -> p `mod` n /= 0) $ takeWhile (\n -> n*n <= p) [3,5..])

primes' :: Integral a => a -> [a]
primes' n = [x | x <- [3,5..n], isPrime' x]

{-
   获取下一个素数
   这里递归的基本条件是找到一个素数
-}
nextPrime n | odd n = if isPrime' n then n else nextPrime (n + 2)
            | otherwise = nextPrime (n + 1)
```

### 埃拉托斯特尼筛法

一种更快速地生成素数的方法，这种生成方法就是埃拉托斯特尼筛法（Eratosthenes sieve）。给定从 2 开始连续的一列数，2 为素数。那么，在 2 之后有 2 为因数的数均不为素数，可以被筛掉。下一个数为 3，3之后的所有为 3 的倍数的数全部被筛掉了，因为 4 为 2 的倍数，这在第1轮中已经被筛掉了，下一个是 5，依此类推。

```haskell
sieve :: Integral t => [t] -> [t]
sieve (p:xs) = p:sieve [x|x<-xs, x `mod` p /=0]
primes' = sieve [2..]
```

## 凯撒加密

凯撒加密的公式 `c = (m+k) mod 26`

```haskell
-- Caesar.hs
module Caesar (encode)  where

-- 默认明文中只有小写字母
import Data.Char (ord, chr, isLower)

char2int :: Char -> Int
char2int c = ord c - ord 'a'

int2char :: Int -> Char
int2char n = chr (ord 'a' + n)

shift :: Int -> Char -> Char
shift n c | isLower c = int2char ((char2int c + n) `mod` 26)
          | otherwise = c

encode :: Int -> String -> String
encode n xs = [shift n x | x<-xs]
```

## 解密

解密针对上述凯撒加密方法。

在英文中26个字母出现的频率已经被统计出来了，如果给定了大量的英文文本，那么文本中每一个字母出现的频率应当是接近那个统计出来的频率，也就是说，其余 25 组移位结果中，最接近这个频率统计的就最可能是明文。（*这里注意，因为用的是频率统计的方法，所以如果没有足够的英文文本，可能是无法得到想要的结果，这种解密方式常常称为**频率攻击***）

```haskell
import Data.Char (isLower)
import Caesar

--计算英文文本频率与统计频率接近程度
chisqr :: [Float] -> [Float] -> Float
chisqr os es = sum [((o - e) ^2) / e | (o, e) <-zip os es]

-- 英文字符 a ~ z 在英文文本中出现的百分比
table :: [Float]
table = [8.2,1.5,2.8,4.3,12.7,2.2,2.0,6.1,7.0,0.2,0.8,4.0,2.4,6.7,7.5,1.9,0.1,6.0,6.3,9.1,2.8,1.0,2.4,0.2,2.0,0.1]

-- 统计某一英文字符在文本出现的次数
count :: Char -> String -> Int
count x xs = length [x' | x' <- xs, x == x']

percent :: Int -> Int -> Float
percent n m = (fromIntegral n / fromIntegral m) * 100

-- 统计文本中有多少个小写英文字母
lowers :: String -> Int
lowers xs = length [x | x <- xs, isLower x]

-- 统计文本中各个字母出现概率
freqs :: String -> [Float]
freqs xs  = [percent (count x xs) n | x <- ['a' .. 'z']]
                            where n = lowers xs

-- 移动列表中的元素所在的位置
rotate :: Int -> [a] -> [a]
rotate n xs = drop n xs ++ take n xs

-- 解密方法
-- 从25组值中找出最小的，它在表中所在的位置就是移动的位数。
-- 首先，得到秘闻中各个英文字符的频率，记为 table'，table' 与原文频率表table依次应用 chisqr，这样就得到了所有的距离，从这些距离中找出最小的，其所在的位置就是解密需要移动的次数
crack :: String -> String
crack xs = encode (-factor) xs
           where
               factor = head (positions (minimum chitab) chitab)	   
               chitab = [chisqr (rotate n table') table | n <- [0..25]]
               table' = freqs xs

-- 元素在列表所在的位置
positions :: Eq a => a -> [a] -> [Int]
positions x xs = [i | (x', i)<- zip xs [0..], x ==x']
```

