# 递归函数

递归函数（recursive function）就是一个函数在自己调用自己，知道一种特殊的情况下，递归才会停止。



## 递归函数的概念

对于一只一般的递归函数，函数的定义可以分为两部分：

1. 递归的基本条件（base case）
2. 递归步骤（recursive step）

递归函数会从给定的值一直调用它自己。在调用的过程中，函数的参数不断发生变化，向基本条件靠拢，调用一直到满足递归的基本条件才停止。

```haskell
{-
阶乘函数
这里的error函数用来返回异常，由于结果可以当做时多态类型 a ，即 String -> a，这样可以让返回的结果和其它人一的类型相协调。
-} 
factorial n | n <  0 = error "n is less that 0"
            | n == 0 = 1
            | otherwise = n * factorial (n -1)
```

```haskell
-- 最大公约函数
import Prelude hiding (gcd)
gcd :: Int -> Int -> Int
gcd x y = if y == 0 then x  else gcd y (mod x y)
```

当然，一个递归函数的基本条件可能有多个。同样，递归步骤也可能不止一个。



## 简单递归函数

定义成方函数power，由于x⁰ = 1(x ≠0) 和 xⁿ = x × xⁿ⁻¹(n⩾1,x≠0)，指数为自然数的乘方可以定义为

```haskell
power, power' :: Int -> Int -> Int
power 0 0 = error "cannot calculate power 0 0 "
power _ 0 = 1
power x n = x * power x (n - 1)

{-
当指数为偶数，由于x²ⁿ = xⁿ × xⁿ，这样 xⁿ 就不需要计算两次，而 n 为奇数时 x²ⁿ⁺¹ = xⁿ × xⁿ × x
优化后的power
-}
power' 0 0 = error "cannot calculate power 0 0 "
power' _ 0 = 1
power' x n | odd n = let p = power' x ((n-1) `div` 2) in x * p * p
           | otherwise = let p = power' x (n `div` 2) in p * p
```



在定义基于列表的递归函数时需要考虑两种情形

1.  列表递归的基本定义，即 []

2.  列表至少有一个元素的情形，即 x : xs 代指余下的列表，并且余下的列表可能为空

   ```haskell
   product' [] = 1   					--(1)
   product' (x:xs) = x * product' xs 	--(2)
   ```

   在使用列表时，(:)为一个函数。将一个元素放在一个列表的第一个位置，它常常被称为列表的构造函数，在有些书中常常写作cons。

   ```haskell
   > let cons = (:)
   > cons 5 [1,2,3]
   [5,1,2,3]

   -- 函数snoc，即 cons 的 逆写
   snoc :: a -> [a] -> [a]
   snoc x [] = [x]
   snoc e (x:xs) = x : snoc e xs
   > snoc 0 [1,2,3]
   [1,2,3,0]

   -- 实现reverse函数
   reverse' :: [a] -> [a]
   reverse' [] = []
   reverse' (x:[]) = [x]
   reverse' xs = (last xs) : (reverse' (init xs))
   > reverse' [1,2,3]
   [3,2,1]

   -- 实现last函数
   last' :: [a] -> a
   last' [] = error "empty list"
   last' [x] = x
   last' (_:xs) = last' xs
   > last' [1,2,3]
   1

   --实现take函数
   take' :: Int -> [a] -> [a]
   take' n _ | n <= 0 = []
   take' _ [] = []
   take' n (x:xs) = x : take' (n-1) xs
   > take' 2 [1,2,3]
   [1,2]

   --实现elem函数
   elem' :: Eq a => a -> [a] -> Bool
   elem' _ [] = False
   elem' e (x:xs) | e == x = True
                  | otherwise = elem' e xs
   > lem' 0 [1,2,3]
   False
   > elem' 1 [1,2,3]
   True

   --实现 Data.List 中的 delete函数
   delete' :: Eq a => a -> [a] -> [a]
   delete' _ [] = []
   delete' e (x:xs) = if e ==x then delete' e xs else x : delete' e xs
   > delete' 2 [4,2,4,6,2]
   [4,4,6]

   --实现drop函数
   drop' :: Int -> [a] -> [a]
   drop' _ [] = []
   drop' n (x:xs) | n <= 0 = x:xs
                  | otherwise = drop' (n - 1) xs

   ```

   ​

## 扩展递归与尾递归

```haskell
-- 阶乘函数
factorial :: Int -> Int
factorial n = if n == 0 then 1 else n * factorial (n - 1)

-- 最大公约函数
mygcd :: Int -> Int -> Int
mygcd x y = if y == 0 then x else mygcd y (mod x y)
```

- 阶乘函数在未达到递归的基本条件前一直在展开，并不能进行任何计算，所有的中间结果将会在暂时存储的内存的栈中，越叠越多。这样的递归称为**扩展递归（augmenting recursion）**，即在递归步中除调用递归函数以外还参与了其它的函数计算。

- 最大公约数这样的函数，不必展开就可以计算，即除递归地调用以外没有任何其他的函数计算，递归调用的结果即为函数的记过，即便递归函数的参数进行了其它的计算，在计算过程中也可以不必展开。像这样的递归被称为**尾递归（tail recursion）**，即在递归的时候不必要向内存中暂存任何值。尾递归是一种特殊的情况。

  ```haskell
  -- 扩展递归
  total :: Num t => [t] -> t
  total [] = 0
  total (x:xs) = x + total xs

  {- 
  	尾递归实现
  	可以看到，利用尾递归可以将代码优化，省去了分配临时空间
  	函数total''中的参数n称为累积器（accumulator）。通常，类机器的类型和函数的结果类型相同。 
  -}
  total'' :: Num t => [t] -> t -> t
  total'' [] n = n
  total'' (x:xs) n = total'' xs (x + n)
  total' xs = total'' xs 0

  {-
  	扩展递归和尾递归的关系
  	total xs + x = total' xs x
  	
  	1，递归基本条件情况下两者的关系
  	total' [] n
  	= total [] + n        {total 与 total' 的等价关系}
  	= 0 + n               {应用加法函数}
  	= n
  	2，递归步骤情况下两者的关系
  	total' (x:xs) n
  	 = (total x + x) + n  {total 与 total' 的等价关系}
  	 = total xs + (x + n) {加法结合律}
  	 = total' xs (x + n) {逆用 total 与 total' 的等价关系}
  -}
  ```

- 表面上，这样的优化本来可以使Haskell不需要更多的空间。可是，Haskell是一个默认设置为惰性求值的语言。 

- 由于惰性求值在使用尾递归时也可能会产品和扩展递归一样的问题，因此，在 total' 函数调用到递归基本条件前，参数 n 只参与和 x 的加法运行，二并不作为结果使用，即 n 的具体值在递归到达基本条件前不需要被计算。因此，Haskell还是会把这些值暂时存于内存中，等到需要的时候才计算。

  ```haskell
  {-
  total'' [1, 2, 3] 0
  =total'' [2, 3] (1 +0)
  =total'' [3] (2 + (1 +0))
  =total'' [] (3+ (2 + (1 +0)))
  =(3+ (2 + (1 +0)))
  =(3+ (2 + 1))
  =(3+ 3)
  =6
  -}
  ```

- 这样，需要使用 **叹号模式（bang pattern）匹配**或者**($!)运算符**来强制Haskell对 total''的第二个参数进行求值。

  ```haskell
  total'' [] n = n
  total'' (x:xs) n = total'' xs $! (x + n)
  -- total'' (x:xs) !n = total'' xs (x + n)
  ```


- **!模式**是强制在参数匹配前来计算参数的值，**!模式**需要在文件首次声明 **{-# LANGUAGE BangPatterns #-}** 语言扩展。而**($!)**则为灾调用函数时计算参数的值。

  ```haskell
  {-
  total'' [1, 2, 3] 0
  =total'' [2, 3] (1 +0)
  =total'' [2, 3] 1
  =total'' [3] (2 + 1)
  =total'' [3] 3
  =total'' [] (3+ 3)
  =total'' [] 6
  =6
  -}
  ```

  ​

## 互调递归

**互调递归（mutual recursion）**是一种特殊的情形，即两个函数的定义分别都用到了对方。

```haskell
even' 0 = True
even' n = odd' (n -1)

odd' 0 = False
odd' n = even' (n - 1)
```

这种互调递归是间接递归的一种特例。