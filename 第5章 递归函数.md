# 递归函数

递归函数（recursive function）就是一个函数在自己调用自己，知道一种特殊的情况下，递归才会停止。



## 递归函数的概念

对于一只一般的递归函数，函数的定义可以分为两部分：

1. 递归的基本条件（base case）
2. 递归步骤（recursive step）

递归函数会从给定的值一直调用它自己。在调用的过程中，函数的参数不断发生变化，向基本条件靠拢，调用一直到满足递归的基本条件才停止。

```haskell
{-
阶乘函数
这里的error函数用来返回异常，由于结果可以当做时多态类型 a ，即 String -> a，这样可以让返回的结果和其它人一的类型相协调。
-} 
factorial n | n <  0 = error "n is less that 0"
            | n == 0 = 1
            | otherwise = n * factorial (n -1)
```

```haskell
-- 最大公约函数
import Prelude hiding (gcd)
gcd :: Int -> Int -> Int
gcd x y = if y == 0 then x  else gcd y (mod x y)
```

当然，一个递归函数的基本条件可能有多个。同样，递归步骤也可能不止一个。



## 简单递归函数

定义成方函数power，由于x⁰ = 1(x ≠0) 和 xⁿ = x × xⁿ⁻¹(n⩾1,x≠0)，指数为自然数的乘方可以定义为

```haskell
power, power' :: Int -> Int -> Int
power 0 0 = error "cannot calculate power 0 0 "
power _ 0 = 1
power x n = x * power x (n - 1)

{-
当指数为偶数，由于x²ⁿ = xⁿ × xⁿ，这样 xⁿ 就不需要计算两次，而 n 为奇数时 x²ⁿ⁺¹ = xⁿ × xⁿ × x
优化后的power
-}
power' 0 0 = error "cannot calculate power 0 0 "
power' _ 0 = 1
power' x n | odd n = let p = power' x ((n-1) `div` 2) in x * p * p
           | otherwise = let p = power' x (n `div` 2) in p * p
```



在定义基于列表的递归函数时需要考虑两种情形

1.  列表递归的基本定义，即 []

2.  列表至少有一个元素的情形，即 x : xs 代指余下的列表，并且余下的列表可能为空

   ```haskell
   product' [] = 1   					--(1)
   product' (x:xs) = x * product' xs 	--(2)
   ```

   在使用列表时，(:)为一个函数。将一个元素放在一个列表的第一个位置，它常常被称为列表的构造函数，在有些书中常常写作cons。

   ```haskell
   > let cons = (:)
   > cons 5 [1,2,3]
   [5,1,2,3]

   -- 函数snoc，即 cons 的 逆写
   snoc :: a -> [a] -> [a]
   snoc x [] = [x]
   snoc e (x:xs) = x : snoc e xs
   > snoc 0 [1,2,3]
   [1,2,3,0]

   -- 实现reverse函数
   reverse' :: [a] -> [a]
   reverse' [] = []
   reverse' (x:[]) = [x]
   reverse' xs = (last xs) : (reverse' (init xs))
   > reverse' [1,2,3]
   [3,2,1]

   -- 实现last函数
   last' :: [a] -> a
   last' [] = error "empty list"
   last' [x] = x
   last' (_:xs) = last' xs
   > last' [1,2,3]
   1

   --实现take函数
   take' :: Int -> [a] -> [a]
   take' n _ | n <= 0 = []
   take' _ [] = []
   take' n (x:xs) = x : take' (n-1) xs
   > take' 2 [1,2,3]
   [1,2]

   --实现elem函数
   elem' :: Eq a => a -> [a] -> Bool
   elem' _ [] = False
   elem' e (x:xs) | e == x = True
                  | otherwise = elem' e xs
   > lem' 0 [1,2,3]
   False
   > elem' 1 [1,2,3]
   True

   --实现 Data.List 中的 delete函数
   delete' :: Eq a => a -> [a] -> [a]
   delete' _ [] = []
   delete' e (x:xs) = if e ==x then delete' e xs else x : delete' e xs
   > delete' 2 [4,2,4,6,2]
   [4,4,6]

   --实现drop函数
   drop' :: Int -> [a] -> [a]
   drop' _ [] = []
   drop' n (x:xs) | n <= 0 = x:xs
                  | otherwise = drop' (n - 1) xs

   ```

   ​

## 扩展递归与尾递归

```haskell
-- 阶乘函数
factorial :: Int -> Int
factorial n = if n == 0 then 1 else n * factorial (n - 1)

-- 最大公约函数
mygcd :: Int -> Int -> Int
mygcd x y = if y == 0 then x else mygcd y (mod x y)
```

- 阶乘函数在未达到递归的基本条件前一直在展开，并不能进行任何计算，所有的中间结果将会在暂时存储的内存的栈中，越叠越多。这样的递归称为**扩展递归（augmenting recursion）**，即在递归步中除调用递归函数以外还参与了其它的函数计算。

- 最大公约数这样的函数，不必展开就可以计算，即除递归地调用以外没有任何其他的函数计算，递归调用的结果即为函数的记过，即便递归函数的参数进行了其它的计算，在计算过程中也可以不必展开。像这样的递归被称为**尾递归（tail recursion）**，即在递归的时候不必要向内存中暂存任何值。尾递归是一种特殊的情况。

  ```haskell
  -- 扩展递归
  total :: Num t => [t] -> t
  total [] = 0
  total (x:xs) = x + total xs

  {- 
  	尾递归实现
  	可以看到，利用尾递归可以将代码优化，省去了分配临时空间
  	函数total''中的参数n称为累积器（accumulator）。通常，类机器的类型和函数的结果类型相同。 
  -}
  total'' :: Num t => [t] -> t -> t
  total'' [] n = n
  total'' (x:xs) n = total'' xs (x + n)
  total' xs = total'' xs 0

  {-
  	扩展递归和尾递归的关系
  	total xs + x = total' xs x
  	
  	1，递归基本条件情况下两者的关系
  	total' [] n
  	= total [] + n        {total 与 total' 的等价关系}
  	= 0 + n               {应用加法函数}
  	= n
  	2，递归步骤情况下两者的关系
  	total' (x:xs) n
  	 = (total x + x) + n  {total 与 total' 的等价关系}
  	 = total xs + (x + n) {加法结合律}
  	 = total' xs (x + n) {逆用 total 与 total' 的等价关系}
  -}
  ```

- 表面上，这样的优化本来可以使Haskell不需要更多的空间。可是，Haskell是一个默认设置为惰性求值的语言。 

- 由于惰性求值在使用尾递归时也可能会产品和扩展递归一样的问题，因此，在 total' 函数调用到递归基本条件前，参数 n 只参与和 x 的加法运行，二并不作为结果使用，即 n 的具体值在递归到达基本条件前不需要被计算。因此，Haskell还是会把这些值暂时存于内存中，等到需要的时候才计算。

  ```haskell
  {-
  total'' [1, 2, 3] 0
  =total'' [2, 3] (1 +0)
  =total'' [3] (2 + (1 +0))
  =total'' [] (3+ (2 + (1 +0)))
  =(3+ (2 + (1 +0)))
  =(3+ (2 + 1))
  =(3+ 3)
  =6
  -}
  ```

- 这样，需要使用 **叹号模式（bang pattern）匹配**或者**($!)运算符**来强制Haskell对 total''的第二个参数进行求值。

  ```haskell
  total'' [] n = n
  total'' (x:xs) n = total'' xs $! (x + n)
  -- total'' (x:xs) !n = total'' xs (x + n)
  ```


- **!模式**是强制在参数匹配前来计算参数的值，**!模式**需要在文件首次声明 **{-# LANGUAGE BangPatterns #-}** 语言扩展。而**($!)**则为灾调用函数时计算参数的值。

  ```haskell
  {-
  total'' [1, 2, 3] 0
  =total'' [2, 3] (1 +0)
  =total'' [2, 3] 1
  =total'' [3] (2 + 1)
  =total'' [3] 3
  =total'' [] (3+ 3)
  =total'' [] 6
  =6
  -}
  ```

  ​

## 互调递归

**互调递归（mutual recursion）**是一种特殊的情形，即两个函数的定义分别都用到了对方。

```haskell
even' 0 = True
even' n = odd' (n -1)

odd' 0 = False
odd' n = even' (n - 1)
```

这种互调递归是间接递归的一种特例。

## 麦卡锡的91函数

```haskell
mc n | n > 100 = n - 10
     | otherwise = mc (mc (n + 11))
```



## 斐波那契数列

```haskell
-- 性能差的版本
fibonacci :: (Num a, Eq a) => a -> a -- 数字类型的模式匹配需要Eq类型类
fibonacci 0 = 1
fibonacci 1 = 1
{-
这种递归函数称为多重递归（multiple recursion）或者多分支递归。
它是一种特殊的递归，即一个函数中递归调用多次。
-}
fibonacci n = fibonacci (n - 1) + fibonacci (n - 2)

fibs n = map fibonacci [0..n-1]

-- 优化性能后的版本
fibStep :: Num a => (a, a) -> (a, a)
fibStep (u, v) = (v, u + v)

fibPair :: (Num a, Eq a) => a -> (a, a)
fibPair 0 = (0, 1)
fibPair n = fibStep (fibPair (n - 1))

fastFib :: (Num a, Eq a) => a -> a
fastFib n =snd (fibPair n)

fastFibs n = map fastFib [0..n-1]

-- 迭代版本
fibs' n = take n (map snd (iterate fibStep (0, 1)))

-- 尾递归版本
fib 0 f1 f2 = f2
fib n f1 f2 = fib (n - 1) f2 (f1 + f2)
fibonacci' n = fib n 0 1
fibs'' n = map fibonacci' [0..n-1]
```

- 斐波那契数列特性，前一个数与后一个数的比值逼近黄金分割值。

  ```haskell
  golden :: Fractional a => Int -> [a]
  golden n = take n (map (\(x,y) -> x/y) (iterate fibStep (0, 1)))

  > golden 30
  [0.0,1.0,0.5,0.6666666666666666,0.6,0.625,0.6153846153846154,0.6190476190476191,0.6176470588235294,0.6181818181818182,0.6179775280898876,0.6180555555555556,0.6180257510729614,0.6180371352785146,0.6180327868852459,0.6180344478216818,0.6180338134001252,0.6180340557275542,0.6180339631667066,0.6180339985218034,0.618033985017358,0.6180339901755971,0.6180339882053251,0.618033988957902,0.6180339886704432,0.6180339887802427,0.618033988738303,0.6180339887543226,0.6180339887482036,0.6180339887505408]

  -- 用Haskell来粗略计算一下黄金分割的值
  > (sqrt 5 - 1) /2
  0.6180339887498949
  ```

  ​

- 斐波那契数列特性，4个相连的整数的内积和外积相差1。F𝗇-₁F𝗇₊₁ - (F𝗇)² = (-1)ⁿ

  ```haskell
  combine :: [(a,a)] ->[(a,a,a)]
  combine ((f1,f2):(f3,f4):fs) = (f1,f2,f4):combine ((f3,f4):fs)
  combine _ = []

  fibPairs :: Int -> [(Int, Int)]
  fibPairs n = map fibPair [1..n]
  > combine $ fibPairs 7
  [(1,1,2),(1,2,3),(2,3,5),(3,5,8),(5,8,13),(8,13,21)]

  difference :: Int -> [Int]
  difference n = map (\(f1,f2,f3)->f1*f3-f2*f2) (combine $ fibPairs n)
  >diffence 10
  [1,-1,1,-1,1,-1,1,-1,1]
  ```

## 十进制数字转换成罗马数字

```haskell
romeNotation :: [String]
romeNotation = ["M","CM","D","CN","C","XC","L","XL","X","IX","V","IV","I"]

romeAmount :: [Int]
romeAmount = [1000,900,500,400,100,90,50,40,10,9,5,4,1]

pair :: [(Int, String)]
pair = zip romeAmount romeNotation

subtrahend :: Int -> (Int, String)
subtrahend n = head (dropWhile (\(a,_) -> a > n) pair)

convert :: Int -> String
convert 0 = ""
convert n = let (rome, m) = subtrahend n
                 in m ++ convert (n-rome)               
> convert 12
"XII"
> convert 109
"CIX"
> convert 4567
"MMMMDLXVII"   

{-
convert 17
= "X"   + convert(17-10)
= "XV"  + convert(7-5)
= "XV"  + convert(2)
= "XVI" + convert(2-1)
= "XVI" + convert(1)
= "XVII
-}

-- 十进制转二进制
decimalToBinary :: Integral a => a -> String
decimalToBinary n | n < 0  = error "unsupported number"
                  | n == 0 = ""
                  | otherwise = let bit= if odd n then "1" else "0" in (decimalToBinary (n `div` 2)) ++ bit
```

##二分法查找

```haskell
-- 需要有序的列表
binarySearch :: (Ord a) => a -> [a] -> Bool
binarySearch _ [] = False
binarySearch a (x:[]) = a == x
binarySearch a xs | m < a = binarySearch a behind
                  | m > a = binarySearch a front
	              | otherwise = True
	              where (front, m:behind) = splitAt (length xs `div` 2) xs

-- 查找并返回元素	        
search :: (Ord a) => a -> [a] -> [a]
search _ [] = []
search a (x:xs) | a == x  = a : search a xs
                | otherwise = search a xs
```

## 汉诺塔

```haskell
{-
假设有n个金片的时候要移动 h(n) 次。

当有一个金片的时候，直接移动就好了，需要 h(1) = 1 次。

当有 n 个金片的时候，需要将n - 1 个金片移动到非目标针上，需要h(n-1)次。至于怎么移动的不需要知道，然后将最底下的金片移动到目标针。最后，再将非目标针上的 n-1 个金片同样用哪个不需要知道的方法移动到目标针上即可。此时也需要 h(n-1)次。

这样，所需要移动的总次数为 h(n) = h(n-1) + 1 + h(n-1) = 2h(n-1) + 1
-}
{-
 参数说明
 金片数；从哪里移动；移动到哪里；移动过程中可以借助哪根针
-} 
move (1, from, to, via) = [(from, to)]
move (n, from, to, via) = move (n-1, from, via, to) ++
                         [(from, to)] ++
			 move (n-1, via, to, from)
hanoi n = move (n, 1, 2, 3)
hanoiCnt n = length $ hanoi n

hanoiCnt' 1 = 1
hanoiCnt' n = 2 * hanoiCnt'(n-1) + 1
```

## 排序算法

### 插入排序（insertion sort）

当列表为空的时候，相当于手里没牌，直接拿就可以了。当手中有了一些有序的牌后，在插入新牌的时，若它比当前比较的牌小，房子啊前边就好，若比当前比较的牌大，只需要递归地将新牌插入到后边的牌里。

```haskell
insert, insert' :: Ord t => t -> [t] -> [t]
insert n [] = [n]
insert n a | n <= h = n : a
           | otherwise = h : (insert n t) 
	   where
	     h = head a; t = tail a
> insert 7 [5,12,10]
[5,7,10,12]

-- 尾递归实现
insert' n [] = [n]
insert' n (x:xs) | n <= x = n : x : xs
                 | otherwise = x : (insert' n xs)  
> insertionSort [] [7,5,12,10]
[5,7,10,12]

-- 扩展递归实现
insertionSort' :: Ord t => [t] -> [t]
insertionSort' [] = []
insertionSort' (x:xs) = insert x (insertionSort' xs)
> insertionSort' [7,5,12,10]
[5,7,10,12]
```

### 冒泡排序（bubble sort）

通过不断地将较大的牌向右交换，重复多次就可以得到一沓有序的牌，每运行到结尾，最后一张牌总是最大的，这个最大的被称为“泡”。

```haskell
-- 交换一个元素
swaps :: Ord t => [t] -> [t]
swaps [] = []
swaps[x] = [x]
swaps (x1:x2:xs) | x1 <= x2 = x1 : swaps (x2:xs)
                 | otherwise = x2 : swaps (x1:xs)

-- 定义一个不动点函数。这个函数可以一直调用 swaps，一只到列表不再发生变化为止
fix :: Eq a => (a -> a) -> a -> a
fix f x = if x == x' then x else fix f x'
                            where x' = f x

-- 冒泡排序不过是一个反复调用 swaps 的函数
bubbleSort :: Ord a => [a] -> [a]
bubbleSort xs = fix swaps xs

> bubbleSort [7,12,3,5,10]
[3,5,7,10,12]

{-
要重复化简一组数据，但是化简后的数据还可以被继续化简，者一类的问题就可以交给 fix 函数来处理,
实质上不动点函数式可以代替顺序式编程语言中的循环的。
-}
-- 相当于把 fix 隐藏在 bubbleSort' 中
bubbleSort' :: Ord a => [a] -> [a]
bubbleSort' xs | swaps xs == xs = xs
               | otherwise bubbleSort' $ swaps xs
--优化版本的冒泡排序
{-
既然最大的已经被交换到了最右边了，就没有必要再使用交换函数了。因此，可以将交换完毕的列表分成前 n-1 项与最后一项，最后一项可以不用动了，再递归调用bubbleSort''，来对前 n-1 项 排序
-}
bubbleSort'' :: Ord a => [a] -> [a]
bubbleSort'' [] = []
bubbleSort'' xs = bubbleSort'' initialElements ++ [lastElement]
                  where swappedxs = swaps xs
		                initialElements = init swappedxs
		                lastElement = last swappedxs
```

### 选择排序（selection sort）

首先找到最小的元素，将其从列表中取出，放于另一个列表的地一个位置，知道所有的元素全部被取光。

```haskell
delete :: Eq t => t -> [t] -> [t]
delete _ [] = []
delete e (x:xs) | e == x = xs
                | otherwise = x : delete e xs
                
selectionSort :: Ord t => [t] -> [t]
selectionSort [] = []
selectionSort xs = mini : selectionSort xs'
                   where mini = minimum xs
		         xs' = delete mini xs
> selectionSort [9,8..1]		         
[1,2,3,4,5,6,7,8,9]
```

### 快速排序（quick sort）

快速排序的基本思路是：取出一张牌，作为一个分界点，然后筛选出比其小的作为一组。比其大的作为一组，分别再用快速排序算法递归地对这两组进行排序后，把分界点与排序好的两组牌进行组合即可。

```haskell
quickSort :: Ord t => [t] -> [t]
quickSort [] = []
quickSort (x:xs) = quickSort mini ++ [x] ++ quickSort maxi
      where mini = filter (<x) xs
            maxi = filter (>=x) xs

> quickSort [4,5,7,2,3,4,6,0,9,8,5,3,1]
[0,1,2,3,3,4,4,5,5,6,7,8,9]

{-
虽然上面的函数很简单，但由两个问题，其一，产生了很多的[]，这个非常好解决，只要加一条 quickSort [x] = [x]即可。其二，当筛选出比 x 小的元素时，要将列表遍历一次，筛选出大于等于 x 的原始时，又要遍历一次。因此，可以定义一个函数，当遍历列表时，一次性地将其分好组，然后被 quickSort 函数所用。这个函数需要一个条件函数来的值如何筛选，然后需要一个列表进行筛选，把结果的两部分当成一个元组返回。
-}

-- 优化后的快速排序
filterSplit :: (a->Bool) -> [a] -> ([a], [a])
filterSplit _ [] = ([], [])
filterSplit f (x:xs) | f x = ((x:l), r)
                     | otherwise = (l, (x:r))
              where (l, r) = filterSplit f xs

quickSort' :: Ord t => [t] -> [t]
quickSort' [] = []
quickSort' [x] = [x]
quickSort' (x:xs) = quickSort' l ++ [x] ++ quickSort' r
      where (l, r) = filterSplit (<x) xs
```

### 归并排序（merge sort）

采用分治法（divide and conquer）的思想。算法的过程大约是：先将牌均匀地分成两部分，将者两部分分别用归并排序算法，然后用一个函数将者两沓有序的牌合并为一沓有序的牌。那么，当用归并排序那两部分的时候，实际上是又将者两部分牌继续均分，然后递归地使用这个排序方法，递归的基本条件是一张牌自己是有序的。

```haskell
merge :: Ord a => [a] -> [a] -> [a]
merge xs [] = xs
merge [] xs = xs
merge (x:xs) (y:yx) | x > y = y:merge (x:xs) yx
                    | otherwise = x:merge xs (y:yx)
                    
msort :: Ord a => [a] -> [a]
msort [x] = [x]
msort xs = merge (msort x1) (msort x2)
  where
    (x1, x2) = halve xs
    halve xs = (take l xs, drop l xs)
    l        = (length xs) `div` 2
```

### 小结

本章所讨论的很多问题，如斐波那契数列、汉诺塔、快速排序与归并排序，都有一个共同点，那就是在用定义时有很多递归调用的分支，比如：

- 斐波那契数列中的 fibonacci n = fibonacci (n - 1) + fibonacci (n - 2) ；
- 汉诺塔中的  move (...) = move(…) ++ [(from, to)] ++ move (…)；
- 快速排序中的 quickSort' (x:xs) = quickSort' l ++ [x] + quickSort' r；
- 归并排序中的 msort xs = merge (msort x1) (msort x2)

​    这样的例子还有很多。这样的递归称为多分枝递归（multi-branched recursion）。在解决问题时，像这样不断地将问题递归地转换成多个小问题的方法称为分治算法（divide and conquer algorithm）。

函数式编程语言是一类非常高级的语言，因为分配与回收内存、移动指针、声明变量等在这里几乎可以全部忽略，并且基于列表这种递归结构的容器来编程的自由度也很大。

### 递归基本条件与程序终止

有这样一类递归函数，无法确定递归基本条件一定可以到达。

```haskell
--对于这个程序，人们还无法证明它是否一定可以到达递归的基本条件
halt :: Integral a => a ->[a]
halt 1 = [1]
halt n | even n = let n' = div n 2 in n':halt n'
       | otherwise = let n' = 3*n+1 in n':halt n'
```

这一类递归函数略又些不同。虽然不能很容易地直接看出或者证明它会停下来，但是他们总被任务是会停下来的，可惜很难给出证明，这就是著名的考拉兹猜想（Collatz conjecture ，也有译作奇偶归一猜想），生成的这个序列称为考拉兹序列（Collatz sequence）。

现在，读者可以知道，递归是否会到达基本条件然后停止，对于程序来说至关重要。

### 递归与不动点

fix函数，他就是不动点函数（fixed point function）。函数的不动点意为当参数应用到这个函数时，结果是这个参数本身。

### 无基本条件递归和惰性求值

在Haskell中有着这样一类的递归：它们并没有递归的基本条件，而仅仅有一个递归步，比如上节中的不动点函数。这些函数载不断的调用自身，但却没有停止条件。

```haskell
-- [1..]
ones = 1:ones

ones
= 1:ones
= 1:(1:ones)
= 1:(1:(1:ones))
....
> take 10 ones
[1,1,1,1,1,1,1,1,1,1]

nature = 0 : map(+1) nature
> take 10 nature
[0,1,2,3,4,5,6,7,8,9]

fibs = (0:1:zipWith (+) fibs (tail fibs))
> take 100 fibs
[0,1,1,2,3,5,8,13,21,34]
```

虽然列表是无穷的。但是通过惰性求值，用`take`会使取前迹象的计算过程停止。

这也就说明了Haskell在计算时并没有完全计算整个列表而是仅仅计算它所需要的部分。

#### 变得懒惰

```haskell
{-
 该函数存在的问题
 问题一：如果一个列表的长度是有限的，另外一个是无限的，那么这个函数则不会停止。
 问题二：因为 length :: [a] -> Int，Int 是有范围的。所以，如果列表的长度大于Int的值，这样就可能会得到负值而导致错误的比较结果。所以，如果处理的列表长度可能超过 Int 的范围，就需要使用 Data.List 中的 genericLength 函数。
-}
shorter :: [a] -> [a] -> [a]
shorter xs ys | x < y = xs
              | otherwise = ys
              where
                  x = length xs
                  y = length ys
                  
--修改后的函数
-- 通过惰性求值的特性，只要有一个列表为空时，就直接返回结果，而不再去计算另一个列表了。这样，即便另外一个列表是无限的，也不会使这个计算卡住。
lazyShorter :: [a] -> [a] -> [a]
lazyShorter xs ys = if short xs ys then xs else ys
              where short [] ys = True
                    short xs [] = False
                    short (x:xs) (y:ys) = short xs ys
```

